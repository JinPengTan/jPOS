plugins {
   id "org.gradlex.extra-java-module-info" version "1.1"
   id "org.owasp.dependencycheck" version "8.3.1"
   // id 'checkstyle'
}

extraJavaModuleInfo {
    automaticModule ('jdom2-2.0.6.jar', 'org.jdom2')
    automaticModule ('javatuples-1.2.jar', 'org.javatuples')
    automaticModule ('bsh-2.0b6.jar', 'bsh')
    automaticModule ('commons-cli-1.5.0.jar', 'org.apache.commons.cli')
    automaticModule ('je-18.3.12.jar', 'com.sleepycat.je')
    automaticModule ('jdbm-1.0.jar', 'org.jdbm')
    automaticModule ('HdrHistogram-2.1.12.jar', 'org.hdrhistogram.HdrHistogram')
    automaticModule ('javassist-3.27.0-GA.jar', 'org.javassist.javassist')
    automaticModule ('xmlunit-1.6.jar', 'xmlunit')
    /*
    module ("sshd-core-${sshdVersion}.jar",  'org.apache.sshd.core', "${sshdVersion}") {
        exports('org.apache.sshd.server.auth.pubkey')
	exports('org.apache.sshd.server.session')
        exports('org.apache.sshd.server.channel')
        exports('org.apache.sshd.server.command')
        exports('org.apache.sshd.server.shell')
    }
    module ("sshd-common-${sshdVersion}.jar",  'org.apache.sshd.common', "${sshdVersion}") {
        exports('org.apache.sshd.server')
        exports('org.apache.sshd.server.keyprovider')
        exports('org.apache.sshd.common')
        exports('org.apache.sshd.common.config.keys')
        exports('org.apache.sshd.common.util.security.eddsa')
        exports('org.apache.sshd.common.cipher')
        exports('org.apache.sshd.common.keyprovider')
        exports('org.apache.sshd.common.util.buffer')
        exports('org.apache.sshd.common.util.security')
        exports('org.apache.sshd.common.config.keys.impl')
    }
    */
}

dependencies {
    api libraries.jdom
    api libraries.javatuples
    api libraries.jline
    api libraries.beanshell
    api libraries.commons_cli
    api libraries.bouncycastle
    api libraries.slf4j_api
    api libraries.hdrhistogram

    implementation libraries.yaml;
    implementation libraries.jdbm
    implementation libraries.sleepycat_je
    // implementation libraries.sshd
    // implementation libraries.eddsa

    testImplementation libraries.commons_lang3
    testImplementation libraries.hamcrest
    testImplementation libraries.xmlunit
    testImplementation libraries.junit
    testImplementation (libraries.mockito) {
        exclude(module: 'hamcrest-core')
    }
    testImplementation libraries.mockito_jupiter

    //  JSONPackager on hold
    //    compile (libraries.jsonsimple) {
    //        exclude(module: 'junit')
    //        exclude(module: 'hamcrest-core')
    //    }
    // Integrated OSGI framework
    // compile 'org.apache.felix:org.apache.felix.framework:5.0.0'
}

def q2ArchiveJarName="${project.name}-${project.version}-q2.jar"
def jposCopySpec = copySpec {
    def cfg = new Properties()
    def target = project.hasProperty('target') ? target : 'devel'
    cfg.put('jarname', q2ArchiveJarName.toString())
    cfg.put('target', target.toString())
    File cfgFile = file("${target}.properties")
    if (cfgFile.exists()) {
        cfgFile.withInputStream{
            cfg.load(it);   
        }
    }
    from(file("src/dist")) {
        exclude 'cfg/*.lmk'
        exclude 'cfg/*.jks'
        exclude 'cfg/*.ks'
        exclude 'cfg/*.ser'
        exclude 'cfg/authorized_keys'
        filter(
            org.apache.tools.ant.filters.ReplaceTokens, 
            tokens: cfg
        )
    }
    from(file("src/dist")) {
        include 'cfg/*.lmk'
        include 'cfg/*.ks'
        include 'cfg/*.jks'
        include 'cfg/*.ser'
        include 'cfg/authorized_keys'
        fileMode 0400
    }
    from(file("${project.buildDir}/libs/${q2ArchiveJarName}"))
    into("lib") {
        from(configurations.runtimeClasspath)
    }
}

task listJars {
    doLast {
        configurations.testCompile.each { File file -> println file.name }
        println "    libsDir: ${project.libsDir}"
        println "libsDirName: ${project.libsDirName}"
        println "libsDirName: ${project.libsDirName}"
        println "    project: ${project.buildDir}"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'jPOS', 
                   'Implementation-Version': project.version
    }
}

java {
   withSourcesJar()
}

task q2jar (type: Jar) {
    dependsOn jar
    archiveClassifier.set('q2')
    from sourceSets.main.output.classesDirs, sourceSets.main.output.resourcesDir
    manifest {
        def manifestClasspath = configurations.runtimeClasspath.collect { "lib/" + it.getName() }.join(' ') 
        attributes 'Implementation-Title': 'jPOS', 
                   'Implementation-Version': project.version,
                   'Main-Class': 'org.jpos.q2.Q2',
                   'Class-Path': manifestClasspath
    }
}

//checkstyle {
//    ignoreFailures = true
//    configFile file("${rootProject.projectDir}/.github/linters/sun_checks.xml")
//}

task sourceJar( type: Jar ) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}

task dist (type: Tar) { 
    dependsOn 'jar', 'assemble', 'sourceJar'
    compression = Compression.GZIP
    includeEmptyDirs true
    with jposCopySpec
    into "jpos-$project.version"
    archiveExtension="tar.gz"
}

task zip (type: Zip) { 
    dependsOn 'q2jar', 'assemble', 'sourceJar'
    includeEmptyDirs true
    includeEmptyDirs true
    with jposCopySpec
    into "jpos-$project.version"
}

task version (type: JavaExec, dependsOn: classes) {
    description = "Display jPOS Version"
    mainClass = 'org.jpos.q2.Q2'
    args = "--version".split().toList()
    classpath sourceSets.main.runtimeClasspath
}

// just an alias to keep the old install task name
task install(dependsOn: publishToMavenLocal) {}


class GitRevisionTask extends DefaultTask
{
    @InputFile @Optional
    File gitHead

    @Input 
    boolean gotHead

    @InputFile @Optional
    File getRefFile() {
        File rf = new File(gitHead.parent,gitHead.text.replace('ref: ', '').trim())
        return rf.exists() ? rf : gitHead
    }

    @OutputFile
    File outputFile

    @TaskAction
    public void writeFile()
    {
        Properties props=new Properties()
        if (gotHead) {
            File ref=getRefFile()
            if (ref != null && ref.exists()) {
                props.put("branch",ref.getName())
                props.put("revision",ref.text.substring(0,7))
            } else {
                props.put("branch", "detached");
                props.put("revision", gitHead.text.substring(0,7))
            }
        } else {
            props.put("branch", "unknown");
            props.put("revision", "unknown");
        }
        props.store(new FileOutputStream(outputFile),"Revision Properties")
    }
}

class BuildTimestampTask extends DefaultTask {
    @OutputFile
    File outputFile

    @TaskAction
    public void writeFile() {
        new File(outputFile.parent).mkdirs()
        Properties props=new Properties()
        props.put("version", project.version);
        props.put("buildTimestamp", new Date().format("yyyy-MM-dd HH:mm:ss z"));
        props.store(new FileOutputStream(outputFile),"Revision Properties")
    }
}

task createRevisionPropertyFile(type: GitRevisionTask) {
    gitHead = "$rootDir/.git/HEAD" as File
    gotHead = gitHead.exists()
    if (!gotHead)
        gitHead = null;
    outputFile = "$sourceSets.main.output.resourcesDir/org/jpos/q2/revision.properties" as File
}

task createBuildTimestampPropertyFile(type: BuildTimestampTask) {
    outputFile = "$sourceSets.main.output.resourcesDir/org/jpos/q2/buildinfo.properties" as File
}

processResources.dependsOn createBuildTimestampPropertyFile, createRevisionPropertyFile
processResources.outputs.upToDateWhen{ false } // issue with Gradle 2.4 confused about UP-TO-DATE (works OK with 2.3)

task installApp(type: Sync) {
    dependsOn q2jar
    delete "${project.buildDir}/install/${project.name}/cfg"
    description 'Installs jPOS based application'
    into { file("${project.buildDir}/install/${project.name}") }
    with jposCopySpec
}

task viewTests (description: 'Open Test Reports') {
    doLast {
        java.awt.Desktop.desktop.browse (
            new File("${buildDir}/reports/tests/test", 'index.html').toURI()
        )
    }
}


test {
  minHeapSize = "4G" 
  maxHeapSize = "8G"
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

